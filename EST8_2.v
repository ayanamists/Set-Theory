(** Based on "Elements of Set Theory" Chapter 8 Part 2 **)
(** Coq coding by choukh, Feb 2021 **)

Require Import ZFC.lib.Class.
Require Export ZFC.EST8_1.
Import OrdinalClass.

(*** EST第八章2：序数操作的性质，Veblen不动点定理 ***)

(* 规范操作的定义域有最大元 *)
Theorem normal_operation_domain_has_maximum :
  ∀ F, F:ᶜ 𝐎𝐍 ⇒ 𝐎𝐍 → normal F → ∀β ⋵ 𝐎𝐍, F 0 ∈ β →
  ∃γ ⋵ 𝐎𝐍, ε_maximum γ (Domain F (RangeAmong F β⁺)).
Proof with eauto.
  intros F HF [Hmono Hcon] β Hoβ Hlt.
  set (λ y, ∃α ⋵ 𝐎𝐍, F α = y) as C.
  assert (Hsur: F :ᶜ 𝐎𝐍 ⟹ C). {
    split. intros ξ Hoξ. exists ξ. split... intros ξ H...
  }
  assert (Hsub: C ⫃ 𝐎𝐍). {
    intros ξ [α [Hoα Heq]]. subst ξ. apply HF...
  }
  assert (Hinj: class_injective F 𝐎𝐍). {
    eapply monotone_operation_injective...
  }
  assert (Hran: ∀x ∈ RangeAmong F β⁺, x ⋵ C). {
    intros ξ Hξ. apply SepE2 in Hξ...
  }
  set (Domain F (RangeAmong F β⁺)) as γ.
  assert (Hoγ: is_ord γ). {
    apply transitive_set_well_ordered_by_epsilon_is_ord.
    - intros x y Hxy Hy.
      eapply domain_spec in Hy as [Hoy HFy]...
      assert (Hox: x ⋵ 𝐎𝐍). eapply ord_is_ords...
      eapply domain_spec... split...
      apply SepI; [apply SepE1 in HFy|exists x; split]...
      eapply ord_trans; revgoals... apply Hmono...
    - apply ords_woset. intros x Hx. eapply domain_spec...
  }
  destruct (ord_is_suc_or_limit γ) as [|Hlim]...
  - destruct H as [μ [Hoμ Heq]]. rewrite Heq.
    exists μ. split... split... intros x Hx.
    apply ord_leq_iff_lt_suc... eapply ord_is_ords; revgoals...
  - exfalso. destruct (classic (γ = 0)) as [|Hne].
    + eapply EmptyNI... exists 0.
      eapply domain_spec... split... apply SepI.
      apply BUnionI1... exists 0. split...
    + apply (ord_irrefl γ)...
      eapply domain_spec... split...
      apply SepI; [|exists γ; split]...
      apply ord_leq_iff_lt_suc... apply HF...
      rewrite Hcon... apply ord_sup_correct.
      * intros x Hx. apply ReplAx in Hx as [α [Hα Heq]].
        subst x. apply HF. eapply ord_is_ords...
      * split... intros x Hx.
        apply ReplAx in Hx as [α [Hα Heq]]. subst x.
        eapply domain_spec in Hα as [Hα HFα]...
        apply SepE1 in HFα. apply ord_leq_iff_lt_suc... apply HF...
Qed.

(* 序数集上界的规范操作等于序数集规范操作的上界 *)
Theorem operation_of_sup_eq_sup_of_operation :
  ∀ F, F:ᶜ 𝐎𝐍 ⇒ 𝐎𝐍 → normal F → ∀ S, is_ords S → S ≠ ∅ →
  F (sup S) = sup (Range F S).
Proof with eauto; try congruence.
  intros F HF [Hmono Hcon] S Hos Hne.
  assert (HoS: sup S ⋵ 𝐎𝐍). apply union_of_ords_is_ord...
  assert (HoF: F (sup S) ⋵ 𝐎𝐍). apply HF...
  assert (HosR: is_ords (Range F S)). {
    intros x Hx. apply ReplAx in Hx as [α [Hoα Heq]].
    subst x. apply HF. apply Hos...
  }
  assert (HoR: sup (Range F S) ⋵ 𝐎𝐍). {
    apply union_of_ords_is_ord...
  }
  apply sub_antisym; apply ord_leq_iff_sub; revgoals...
  - apply ord_sup_correct... split...
    intros ξ Hξ. apply ReplAx in Hξ as [α [Hoα Heq]].
    subst ξ. apply ord_sup_is_ub in Hos as [Ho Hle].
    apply Hle in Hoα as []; [left|right]... apply Hmono...
  - destruct (ord_is_suc_or_limit (sup S)) as [|Hlim].
    + apply ord_sup_is_ub...
    + apply ord_sup_correct... eapply ReplI.
      apply sup_of_ords_is_suc_impl_in_ords...
    + destruct (classic (sup S = ∅)). {
        rewrite H. apply union_empty_iff in H as []...
        apply ord_sup_is_ub... rewrite H.
        eapply ReplI. apply SingI.
      }
      rewrite Hcon... apply ord_sup_correct.
      * intros x Hx. apply ReplAx in Hx as [α [Hα Heq]].
        subst x. apply HF. apply (ord_is_ords (sup S))...
      * split... intros ξ Hξ. left.
        apply ReplAx in Hξ as [α [Hα Heq]]. subst ξ.
        apply UnionAx in Hα as [γ [Hγ Hα]].
        apply UnionAx. exists (F γ). split.
        eapply ReplI... apply Hmono... apply Hos...
Qed.

(* ex8_5 单调操作的值不小于原值 *)
Lemma monotone_operation_leq :
  ∀ F, F:ᶜ 𝐎𝐍 ⇒ 𝐎𝐍 → monotone F → ∀α ⋵ 𝐎𝐍, α ⋸ F α.
Proof with auto.
  intros F HF Hmono. unfold all_in_class, all.
  apply transfinite_induction_schema_on_ordinals.
  intros α Hoα IH. apply ord_leq_iff_not_gt... apply HF...
  intros HFα. apply Hmono in HFα as HFFα...
  apply IH in HFα as [].
  eapply ord_not_lt_gt; revgoals; eauto; try repeat apply HF...
  eapply ord_not_lt_self; revgoals; eauto; try repeat apply HF...
Qed.

Import 𝐎𝐍Operation.

(* Veblen不动点定理：规范操作存在任意大的不动点 *)
Theorem Veblen_fixed_point : ∀ F, F:ᶜ 𝐎𝐍 ⇒ 𝐎𝐍 → normal F →
  ∀β ⋵ 𝐎𝐍, ∃γ ⋵ 𝐎𝐍, F γ = γ ∧ β ⋸ γ.
Proof with neauto; try congruence.
  intros F HF Hnml β Hoβ.
  assert (H := Hnml). destruct H as [Hmono Hcon].
  eapply monotone_operation_leq in Hoβ as H...
  destruct H as [Heq|Hlt]; revgoals. exists β. split...
  set (Operation β F) as f.
  set {f | n ∊ ω} as S.
  assert (Hne: S ≠ ∅). {
    apply EmptyNI. exists (f 0). eapply ReplI...
  }
  assert (Hos: is_ords S). {
    intros x Hx. apply ReplAx in Hx as [n [Hn Hfn]]. subst x.
    apply operation_operative... apply nat_is_ord...
  }
  exists (sup S). repeat split.
  - apply union_of_ords_is_ord...
  - rewrite operation_of_sup_eq_sup_of_operation...
    apply ExtAx. split; intros Hx.
    + apply FUnionE in Hx as [α [Hα Hx]].
      apply ReplAx in Hα as [n [Hn Hfn]]. subst α.
      apply UnionAx. exists (f n⁺). split.
      eapply ReplI... apply ω_inductive...
      unfold f. rewrite operation_suc... apply nat_is_ord...
    + apply UnionAx in Hx as [y [Hy Hx]].
      apply ReplAx in Hy as [n [Hn Hfn]]. subst y.
      apply (FUnionI _ _ (f n)). eapply ReplI...
      assert (Hof: is_ord (f n)). {
        apply operation_operative... apply nat_is_ord...
      }
      eapply monotone_operation_leq in Hof as H...
      destruct H... eapply ord_trans... apply HF...
  - apply ord_sup_is_ub...
    apply ReplAx. exists 0. split...
    unfold f. rewrite operation_0...
Qed.
